apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext.minimumSdkVersion = 16
ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0


android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.codebusters.appsmanager"
        minSdkVersion minimumSdkVersion
        targetSdkVersion 26
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        //resConfigs "en"

        compileOptions {
            incremental = false
        }
        testOptions {
            unitTests.returnDefaultValues = true
        }
    }


    signingConfigs {
        release {
            try {
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
                storeFile file(KEY_STORE)
            }
            catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }

        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
}


private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 +
            ext.versionMajor * 100000 +
            ext.versionMinor * 1000 +
            ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:26.1.0'
    testImplementation 'junit:junit:4.12'
    testCompile "org.mockito:mockito-all:1.10.19"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'


    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:preference-v14:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"

    implementation "com.arello-mobile:moxy:$moxyLibraryVersion"
    implementation "com.arello-mobile:moxy-android:$moxyLibraryVersion"
    implementation "com.arello-mobile:moxy-app-compat:$moxyLibraryVersion"
    kapt "com.arello-mobile:moxy-compiler:$moxyLibraryVersion"

    implementation "com.afollestad.material-dialogs:core:$materialDilaogLibrary"
    implementation "com.afollestad.material-dialogs:commons:$materialDilaogLibrary"

    compile("com.github.hotchemi:permissionsdispatcher:3.0.1") {
        exclude module: "support-v13"
    }
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:3.0.1'

    implementation('com.mikepenz:materialdrawer:6.0.1@aar') {
        transitive = true
    }
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'com.futuremind.recyclerfastscroll:fastscroll:0.2.5'
    implementation 'com.github.aakira:expandable-layout:1.6.0@aar'
    implementation 'com.github.clans:fab:1.6.4'

    compile "io.reactivex.rxjava2:rxjava:2.1.6"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
}

repositories {
    mavenCentral()
}

kapt {
    generateStubs = true
}

